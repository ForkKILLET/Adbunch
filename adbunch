#!/bin/zsh
# vi: set ft=zsh:

source ${0:A:h}/src/utils/color.sh
source ${0:A:h}/src/utils/allscripts.sh

envs=(
    ./data/adbunch{,.env}
    ./adbunch{,.env}
    ${0:A:h}/data/adbunch{,.env}
    ${0:A:h}/adbunch{,.env}
    ~/.config/adbunch{,.env}
    ~/.config/adbunch/env
)

for f in $envs; do
	if [[ -f $f ]]; then
		[[ -z "$QU" ]] && echo "$(green using config): ${f:A}"
		source $f
		break
	fi
done

LOG="${LOG:-./log}"
if [[ ! -f $LOG ]]; then
	[[ -z "$QU" ]] && echo "$(green creating log): $LOG"
	touch "$LOG"
fi

if [[ -z "$1" ]]; then
	echo "$(blue Adbunch) ver $(blue $(<${0:A:h}/version.txt)) by $(blue ForkKILLET '<fork_killet@qq.com>')"
	echo "$(green all scripts): $(alls)"
else
	script="${0:A:h}/src/$1.sh"

	if [[ -f $script ]]; then
		"$script" $@[2,-1]
	else
		echo "$(red unknown command): '$1'"
	fi
fi
